# Desvendando a Infraestrutura como C√≥digo: Terraform e AWS

Este projeto visa capacit√°-lo a criar e gerenciar sua infraestrutura na nuvem AWS de forma automatizada, utilizando o poder do Terraform. Prepare-se para uma jornada pr√°tica na Infraestrutura como C√≥digo!

Para sua conveni√™ncia, este guia est√° dispon√≠vel em [formato PDF](docs/README.pdf) e [formato HTML](docs/README.html), ideal para consulta offline.

## Sum√°rio

-   [Prepare o Terreno](#prepare-o-terreno)
-   [Hands On](#hands-on)
-   [Finalizando e Limpando](#finalizando-e-limpando)
-   [Quer Ir Al√©m?](#quer-ir-al√©m)

## Prepare o Terreno

Antes de come√ßar, garanta que voc√™ tenha o seguinte configurado:

1.  **Terraform:** Instale o Terraform. [Baixe aqui](https://developer.hashicorp.com/terraform/downloads?product_intent=terraform).
    * *Se usa Windows:* Consulte este [guia espec√≠fico](docs/Como%20Instalar%20o%20Terraform%20no%20Windows.pdf).
2.  **AWS CLI:** Instale o AWS CLI. [Siga este link](https://docs.aws.amazon.com/cli/latest/userguide/getting-started-install.html).
3.  **Credenciais AWS:** Crie um usu√°rio na AWS com chaves de acesso e configure-as localmente. O [passo a passo](docs/Cria√ß√£o%20de%20usu√°rio%20na%20AWS%20e%20chave%20de%20acesso%20para%20vincular%20ao%20AWS%20CLI%20na%20sua%20maquina.pdf) est√° aqui.
    * Ap√≥s criar, execute `aws configure` no seu terminal.

## Hands On

Vamos provisionar sua infraestrutura! ü•≥

1.  **Obtenha o C√≥digo:**
    Clone o reposit√≥rio ou baixe o ZIP.

    > [!TIP]
    > Prefere GitHub? Use `https://github.com/seu-reposit√≥rio.git`.

    ```bash
    git clone https://github.com/skvanderson/Desafio-Terraform
    cd iac-com-terraform-e-aws
    ```

    > [!NOTE]
    > Sem Git? [Baixe o ZIP](https://github.com/skvanderson/Desafio-Terraform/-/archive/main/Desafio-Terraform.zip) e descompacte.

2.  **Abra no VS Code:**
    Navegue at√© a pasta do projeto e abra-o:

    ```bash
    code .
    ```

3.  **Crie e Preencha o arquivo `provider.tf`:**
    Crie o arquivo `provider.tf` e insira o seguinte conte√∫do. Ele define o provedor de nuvem (AWS) e a regi√£o de implanta√ß√£o.

    ```hcl
    terraform {
        required_providers {
            aws = {
                source  = "hashicorp/aws"
                version = "~> 5.0"
            }
        }
    }

    provider "aws" {
        region = "us-east-1"
    }
    ```

4.  **Crie e Preencha o arquivo `key_pair.tf`:**
    Crie o arquivo `key_pair.tf` e adicione este c√≥digo. Ele cria um par de chaves SSH, gerando uma chave privada localmente para acesso seguro √† sua inst√¢ncia.

    ```hcl
    # Gera uma chave privada RSA de 4096 bits
    resource "tls_private_key" "rsa_key" {
        algorithm = "RSA"
        rsa_bits  = 4096
    }

    # Cria o Key Pair na AWS usando a chave p√∫blica gerada
    resource "aws_key_pair" "ec2_key_pair" {
        key_name   = "ec2-instance-key" # Nome do key pair na AWS
        public_key = tls_private_key.rsa_key.public_key_openssh
    }

    # Salva a chave privada em um arquivo local
    resource "local_file" "private_key_pem" {
        content  = tls_private_key.rsa_key.private_key_pem
        filename = "${path.module}/ec2-instance-key.pem"

        # Define as permiss√µes do arquivo para que apenas o propriet√°rio possa ler e escrever
        file_permission = "0600"
    }
    ```

5.  **Crie e Preencha o arquivo `security_group.tf`:**
    Crie o arquivo `security_group.tf` com o conte√∫do abaixo. Ele configura os grupos de seguran√ßa (firewall) para controlar o tr√°fego de rede para sua inst√¢ncia EC2.

    ```hcl
    # 1. Security Group para liberar a porta 80 (HTTP) para qualquer origem
    resource "aws_security_group" "http_sg" {
        name        = "allow-http-sg"
        description = "Allow HTTP inbound traffic"

        ingress {
            from_port   = 80
            to_port     = 80
            protocol    = "tcp"
            cidr_blocks = ["0.0.0.0/0"]
        }

        tags = {
            Name = "allow-http"
        }
    }

    # 2. Security Group para liberar a porta 22 (SSH) para um IP espec√≠fico
    resource "aws_security_group" "ssh_sg" {
        name        = "allow-ssh-sg"
        description = "Allow SSH inbound traffic from a specific IP"

        ingress {
            from_port   = 22
            to_port     = 22
            protocol    = "tcp"
            cidr_blocks = [var.meu_ip_publico]
        }

        tags = {
            Name = "allow-ssh"
        }
    }

    # 3. Security Group para liberar todo o tr√°fego de sa√≠da (Egress)
    resource "aws_security_group" "egress_all_sg" {
        name        = "allow-all-egress-sg"
        description = "Allow all outbound traffic"

        egress {
            from_port   = 0
            to_port     = 0
            protocol    = "-1" # "-1" representa todos os protocolos
            cidr_blocks = ["0.0.0.0/0"]
        }

        tags = {
            Name = "allow-all-egress"
        }
    }
    ```

6.  **Crie e Preencha o arquivo `data.tf`:**
    Crie o arquivo `data.tf` e insira este conte√∫do. Ele define uma fonte de dados para buscar a AMI (Amazon Machine Image) mais recente do Amazon Linux 2.

    ```hcl
    data "aws_ami" "amazon_linux" {
        most_recent = true
        owners      = ["amazon"]

        filter {
            name   = "name"
            values = ["amzn2-ami-hvm-*-x86_64-gp2"]
        }
    }
    ```

7.  **Crie e Preencha o arquivo `ec2.tf`:**
    Crie o arquivo `ec2.tf` com o c√≥digo abaixo. Ele configura e provisiona a inst√¢ncia EC2 que servir√° como seu servidor web.

    ```hcl
    # Cria a inst√¢ncia EC2
    resource "aws_instance" "web_server" {
        ami           = data.aws_ami.amazon_linux.id
        instance_type = "t2.micro"
        user_data     = base64encode(file("user_data.sh"))

        # Define o key pair para a inst√¢ncia
        key_name      = aws_key_pair.ec2_key_pair.key_name

        # Associa os 3 Security Groups √† inst√¢ncia
        vpc_security_group_ids = [
            aws_security_group.http_sg.id,
            aws_security_group.ssh_sg.id,
            aws_security_group.egress_all_sg.id
        ]

        tags = {
            Name = "WebServer-DVP"
        }
    }
    ```

8.  **Crie e Preencha o arquivo `outputs.tf`:**
    Crie o arquivo `outputs.tf` e adicione este conte√∫do. Ele especifica quais informa√ß√µes (como o IP p√∫blico da inst√¢ncia e a URL do site) ser√£o exibidas ap√≥s a aplica√ß√£o bem-sucedida do Terraform.

    ```hcl
    # Bloco para exibir o IP p√∫blico da inst√¢ncia ap√≥s a cria√ß√£o
    output "instance_public_ip" {
      description = "IP p√∫blico da inst√¢ncia EC2"
      value       = aws_instance.web_server.public_ip
    }

    output "website_url" {
        description = "URL do site provisionado."
        value       = "http://${aws_instance.web_server.public_ip}"
    }
    ```

9.  **Crie e Preencha o arquivo `variables.tf`:**
    Crie o arquivo `variables.tf` com o c√≥digo abaixo. Ele declara vari√°veis que podem ser facilmente configuradas, como o seu IP p√∫blico, usado para restringir o acesso SSH.

    ```hcl
    variable "meu_ip_publico" {
        type        = string
        description = "Endere√ßo IP p√∫blico para o Security Group SSH"
        # IMPORTANTE: Substitua pelo seu endere√ßo IP p√∫blico
        # Para saber o seu IP p√∫blico, acesse [https://www.whatismyip.com/] ou (https://meuip.com.br/) lembrando √© IP do seu Provedor n√£o do seu Roteador
        default     = "203.0.113.25/32" (matenha o /32)
    }
    ```

    > **Aten√ß√£o:** No arquivo `variables.tf`, lembre-se de substituir o IP `default` pelo *seu IP p√∫blico atual*. Para descobri-lo, voc√™ pode visitar [https://www.whatismyip.com/](https://www.whatismyip.com/), [https://meuip.com.br//](https://meuip.com.br/).

10. **Execute o Terraform:**
    No terminal, dentro do diret√≥rio do projeto, rode os comandos em sequ√™ncia:

    ```bash
    terraform init
    terraform plan
    terraform apply
    ```
    * Confirme a execu√ß√£o digitando `yes` quando `terraform apply` perguntar.

11. **Verifique sua Infraestrutura:**
    Ap√≥s o `apply` ser conclu√≠do com sucesso, o Terraform exibir√° o IP p√∫blico da sua nova inst√¢ncia EC2 e a URL completa do seu site.

    > [!WARNING]
    > Navegadores modernos podem tentar redirecionar para HTTPS. Como este site n√£o est√° configurado para HTTPS, **voc√™ deve digitar `http://`** antes do IP na barra de endere√ßos do seu navegador para acess√°-lo corretamente.

    Seu site deve aparecer assim:

    ![Site no Ar](docs/images/site.png)

## Finalizando e Limpando

1.  **Destrua a Infraestrutura:**
    Quando n√£o precisar mais, remova todos os recursos criados com:

    ```bash
    terraform destroy
    ```
    * Confirme com `yes` quando solicitado.

    > [!NOTE]
    > **Sempre** destrua sua infraestrutura ao finalizar para evitar custos inesperados.

## Quer Ir Al√©m?

Explore a documenta√ß√£o para aprofundar seus conhecimentos:

* [Documenta√ß√£o Terraform](https://developer.hashicorp.com/terraform)
* [Provider AWS no Terraform](https://registry.terraform.io/providers/hashicorp/aws/latest/docs)
* [Lista de Providers Terraform](https://registry.terraform.io/browse/providers)
* [Documenta√ß√£o AWS (Portugu√™s)](https://docs.aws.amazon.com/pt_br/)
